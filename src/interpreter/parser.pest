main = {
    SOI ~ file_line* ~ eoi
}

file_line = { (draw | decor | decl | config_line | save_file) ~ ";" }

decl       = { decl_left ~ "=" ~ decl_right }
decl_left  = { destruct | direct }
direct     = { any_id }
destruct   = { any_id ~ "," ~ any_id }
decl_right = { coord | expr | arg | eval }

expr   =  { method ~ args }
method = @{ punc_alpha+ }
args   =  { (arg ~ ",")* ~ arg }

arg = {
    trig
  | common_obj
  | numeric
  | eval
}

draw       =  { "draw" ~ (draw_step ~ ",")* ~ draw_step }
draw_step  =  { draw_obj ~ ("[" ~ configs ~ "]")? }
draw_obj   =  {
    polygon
  | arc
  | arc_o
  | common_obj
}
decor      = { "decor" ~ (decor_step ~ ",")* ~ decor_step }
decor_step = { decor_obj ~ ":" ~ decoration ~ ("[" ~ configs ~ "]")? }
decor_obj  = {
    polygon
  | angle_3p
  | arc
  | arc_o
  | common_obj
}
decoration = @{ punc_alpha+ }

config_line  =  { "config" ~ configs }
configs      =  { (config ~ ",")* ~ config }
config       =  { config_name ~ "=" ~ config_value }
config_name  = @{ ASCII_ALPHA_LOWER+ }
config_value =  { rich_number | boolean | string }

save_file = { "save" ~ raw_string }

trig       = ${ point_id ~ point_id ~ point_id }
line_2p    =  { point_id ~ point_id }
circ_3p    =  { "@(" ~ point_id ~ point_id ~ point_id ~ ")" }
circ_or    =  { "@(" ~ point_id ~ "," ~ numeric ~ ")" }
circ_oa    =  { "@(" ~ point_id ~ "," ~ point_id ~ ")" }
circ_diam  =  { "@(" ~ point_id ~ point_id ~ ")" }
polygon    = ${ point_id ~ "-" ~ (point_id ~ "-")+ ~ point_id }
arc        = ${ point_id ~ "~" ~ point_id ~ "~" ~ point_id }
arc_o      = ${ point_id ~ "~(" ~ point_id ~ ")~" ~ point_id }
common_obj =  {
    line_2p
  | circ_3p
  | circ_or
  | circ_oa
  | circ_diam
  | any_id
}

linear      = { line_2p | common_id }
distance    = { ("|" ~ point_id ~ point_id ~ "|") | ("|(" ~ (linear | point_id) ~ "," ~ linear ~ ")|") }
angle_3p    = { "<" ~ point_id ~ point_id ~ point_id }
angle_2l    = { "<(" ~ linear ~ "," ~ linear ~ ")" }
numeric     = { distance | angle_3p | angle_2l | rich_number | common_id }

coord       = { "(" ~ (ortho_coord | polar_coord) ~ ")" }
ortho_coord = { numeric ~ "," ~ numeric }
polar_coord = { numeric ~ ":" ~ numeric }

eval = { "$" ~ (!"$" ~ ANY)* ~ "$" }

raw_string  = @{ (!(NEWLINE | ";") ~ ANY)* }
rich_number =  { degree | number }
boolean     =  { "true" | "false" }
degree      = @{ number ~ "deg" }
number      = @{ ("+" | "-")? ~ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? }
string      = ${ "\"" ~ str_inner ~ "\"" }
str_inner   = @{ char* }
char        =  {
    !("\"" | "\\") ~ ANY
  | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
  | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
punc_alpha  = _{
    ASCII_ALPHA
  | "#"
  | "@"
  | "*"
  | "<"
  | "-"
  | ">"
  | "!"
  | "?"
  | "/"
  | "\\"
  | "|"
}

point_id  = @{ ASCII_ALPHA_UPPER ~ (ASCII_ALPHA_LOWER | ASCII_DIGIT)* ~ "'"? }
common_id = @{ (ASCII_ALPHA_LOWER | "_") ~ (ASCII_ALPHA_LOWER | ASCII_DIGIT)* ~ "'"? }
any_id    =  { point_id | common_id }

eoi = _{ !ANY }
WHITESPACE = _{ (NEWLINE | " " | "\t")+ }
