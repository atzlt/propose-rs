main = {
    SOI ~ file_line* ~ EOI
}

file_line = _{ (draw | decor | decl_line | config_line | save_file) ~ ";" }

decl_line =  { decl_left ~ "=" ~ (coord | expr | eval) }
decl_left = _{ direct | destruct }
direct    =  { any_id }
destruct  =  { any_id ~ "," ~ any_id }

expr   =  { method ~ args }
method = @{ punc_alpha+ }
args   =  { (arg ~ ",")* ~ arg }

arg = _{
    trig
  | common_obj
  | rich_number
  | eval
}

draw       =  { "draw" ~ (draw_obj ~ ",")* ~ draw_obj }
decor      =  { "decor" ~ (decor_step ~ ",")* ~ decor_step }
decor_step = _{ draw_obj ~ ":" ~ decoration }
draw_obj   =  {
    polygon
  | arc
  | arc_o
  | common_obj
}
decoration = @{ punc_alpha+ }

config_line  =  { "config" ~ (config ~ ",")* ~ config }
config       =  { config_name ~ "=" ~ config_value }
config_name  = @{ ASCII_ALPHA_LOWER+ }
config_value =  { rich_number | boolean | string }

save_file = { "save" ~ raw_string }

trig       = ${ point_id ~ point_id ~ point_id }
line_2p    =  { point_id ~ point_id }
circ_3p    =  { "@(" ~ point_id ~ point_id ~ point_id ~ ")" }
circ_or    =  { "@(" ~ point_id ~ "," ~ common_id ~ ")" }
circ_oa    =  { "@(" ~ point_id ~ "," ~ point_id ~ ")" }
circ_diam  =  { "@(" ~ point_id ~ point_id ~ ")" }
polygon    = ${ point_id ~ "-" ~ (point_id ~ "-")+ ~ point_id }
arc        = ${ point_id ~ "~" ~ point_id ~ "~" ~ point_id }
arc_o      = ${ point_id ~ "~(" ~ point_id ~ ")~" ~ point_id }
common_obj = _{
    line_2p
  | circ_3p
  | circ_or
  | circ_oa
  | circ_diam
  | point_id
  | common_id
}

coord       = _{ "(" ~ (ortho_coord | polar_coord) ~ ")" }
ortho_coord =  { number ~ "," ~ number }
polar_coord =  { rich_number ~ "," ~ number }

eval       = _{ "$" ~ eval_inner ~ "$" }
eval_inner =  { (!"$" ~ ANY)* }

raw_string  = @{ (!(NEWLINE | ";") ~ ANY)* }
rich_number = _{ degree | number }
boolean     =  { "true" | "false" }
degree      = @{ number ~ "deg" }
number      =  { ("+" | "-")? ~ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? }
string      = ${ "\"" ~ str_inner ~ "\"" }
str_inner   = @{ char* }
char        =  {
    !("\"" | "\\") ~ ANY
  | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
  | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
punc_alpha  = _{
    ASCII_ALPHA
  | "#"
  | "@"
  | "*"
  | "<"
  | "-"
  | ">"
  | "!"
  | "?"
  | "/"
  | "\\"
  | "|"
}

point_id  = @{ ASCII_ALPHA_UPPER ~ (ASCII_ALPHA_LOWER | ASCII_DIGIT | "_")* ~ "'"? }
common_id = @{ (ASCII_ALPHA_LOWER | "_") ~ (ASCII_ALPHA_LOWER | ASCII_DIGIT | "_")* ~ "'"? }
any_id    = _{ point_id | common_id }

WHITESPACE = _{ (NEWLINE | " " | "\t")+ }
